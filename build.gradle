plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.pezzo'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

    // Le librerie che vuoi includere nel plugin finale
    implementation 'com.github.Revxrsal.Lamp:common:3.3.6'
    implementation 'com.github.Revxrsal.Lamp:bukkit:3.3.6'
    implementation 'com.github.Revxrsal.Lamp:brigadier:3.3.6'
}

// 2. Configurazione dello ShadowJar
shadowJar {
    archiveClassifier.set('')
    relocate 'revxrsal.commands', 'me.pezzo.abilityPlugin.libs.lamp'
}

// 3. Collega il build allo shadowJar
tasks.assemble {
    dependsOn(tasks.shadowJar)
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}